# `ckup = require \ckup` in CommonJS, `self.Ckup` in browser etc.
ckup = exports ? @Ckup = {}
ckup import
  VERSION: \0.1.8b

  render: (template, mixin) -> @renderer(template) mixin

  # Creates
  renderer: (template) ->
    template = @compile template unless typeof template is \function
    # a renderer function that
    (mixin) ~>
      # clones `ckup`
      me = ^this
      # with optional `mixin` properties,
      me <<< mixin if mixin
      # `.call`s `template` on the clone,
      template.call me
      # and returns the rendered string.
      me._

  # Compiles a Coco `code` into a template function,
  # enabling `@`-less style using `with` hack.
  compile: (code) ->
    Function 'with(this)' + (Coco ? require \coco)compile "#code"

  # `@_` holds the code being built.
  _: ''

  # Renders an object as CSS rules a la <https://github.com/aeosynth/ccss>.
  css: (rules) ->
    code = ''
    rules.=call this if typeof rules is \function
    for selector, children in rules
      children.=call this if typeof children is \function
      children <<<< kv for kv of []concat that if delete children.mixin
      subrules = declarations = selectors = ''
      for key, val in children
        switch typeof val
        case \string \number
          # _$_-prefixed properties are expanded as vendor-prefixed.
          if (key = @decamelize key)charAt() is \$
            key.=slice 1
            declarations += "  -#v-#key: #val;\n" for v of @VENDORS
          declarations += "  #key: #val;\n"
        default
          ss = []; selectors ||= selector.split @COMMA
          ss.push "#s #k" for s of selectors for k of key.split @COMMA
          (subrules ||= {})[ss.join ', '] = val
      declarations and code += "#selector {\n#declarations}\n"
      subrules     and code += @css subrules
    code

  # Renders a function as JavaScript.
  coco: let utilities = {
    # Creates an object's prototypal child, ensuring `__proto__`.
    clone: '''function(it){
      function fun(){} fun.prototype = it;
      return new fun;
    }'''
    # Sets up `.prototype` between a pair of constructors
    # as well as `.constructor` and `.superclass` references.
    extend: '''function(sub, sup){
      function fun(){} fun.prototype = (sub.superclass = sup).prototype;
      (sub.prototype = new fun).constructor = sub;
      if (typeof sup.extended == 'function') sup.extended(sub);
      return sub;
    }'''

    # Creates a bound method.
    bind: '''function(obj, key){
      return function(){ return obj[key].apply(obj, arguments) };
    }'''

    # Copies properties from right to left.
    import: '''function(obj, src){
      var own = {}.hasOwnProperty;
      for (var key in src) if (own.call(src, key)) obj[key] = src[key];
      return obj;
    }'''
    importAll: '''function(obj, src){
      for (var key in src) obj[key] = src[key];
      return obj;
    }'''

    repeatString: '''function(str, n){
      for (var r = ''; n > 0; (n >>= 1) && (str += str)) if (n & 1) r += str;
      return r;
    }'''
    repeatArray: '''function(arr, n){
      for (var r = []; n > 0; (n >>= 1) && (arr = arr.concat(arr)))
        if (n & 1) r.push.apply(r, arr);
      return r;
    }'''

    of: '''function(x, arr){
      var i = 0, l = arr.length >>> 0;
      while (i < l) if (x === arr[i++]) return true;
      return false;
    }'''

    # Shortcuts to speed up the lookup time for native methods.
    split    : "''.split"
    replace  : "''.replace"
    toString : '{}.toString'
    join     : '[].join'
    slice    : '[].slice'
  }
    (...args, fn) ->
      fn = "#fn"
      preamble = ''
      for key, val in utilities
        if -1 is not fn.search "__#key"
          preamble += "var __#key = #val;"
      ";#{preamble}(#fn).apply(this, #{JSON.stringify args});"

  COMMA   : /\s*,\s*/
  VENDORS : <[ webkit moz ms o ]>

  # Turns a string HTML-safe.
  quote: let re = /[&<>\"\']/g
    function fn
      switch it
      case \& then '&amp;'
      case \< then '&lt;'
      case \> then '&gt;'
      case \" then '&#34;'
      case \' then '&#39;'
    -> "#it"replace re, fn

  decamelize: let re = /[A-Z]/g
    function fn then \- + it.toLowerCase()
    -> "#it"replace re, fn

  # Appends a DTD declaration.
  doctype : !-> @_ += "<!DOCTYPE #it>"
  # Appends a text as is.
  raw     : !-> @_ += it
  # Appends a text node.
  text    : !-> @_ += @quote it
  # Appends an HTML entity.
  entity  : !-> @_ += "&#{ if typeof it is \number then \# + it else it };"

  # Builds an HTML element.
  #
  # - `args`: A list of element content, which can be either of
  #   - an object containing key-value pairs of attributes
  #   - a function which will be called recursively
  #   - any existent value
  # - `tailless`: Indicates end tag omission.
  element: !(name, args, tailless) ->
    code = \< + name; bodies = []
    for arg of args
      if typeof arg is \object
        code += " #key=\"#{ @quote val }\"" for key, val in arg
      else bodies.push arg
    if tailless
      @_ += "#code/>"
      return
    @_ += "#code\n>"
    for body of bodies
      body.=call this if typeof body is \function
      @_ += body if body?
    @_ += "</#name>"

  # Shortcut to `@a {href: url}, txt`.
  A: !(url, txt) ->
    @element \a [...@@] <<< [href: url, txt or @quote url]

# Each tag-named method delegates to `@element`.
# ref. <http://dev.w3.org/html5/spec/Overview.html>
for tag of <[
  a abbr address area article aside audio
  b base bdi bdo blockquote body br button
  canvas caption cite code col colgroup command
  datalist dd del details dfn div dl dt em embed
  fieldset figcaption figure footer form
  h1 h2 h3 h4 h5 h6 head header hgroup hr html i iframe img input ins
  kbd keygen label legend li link map mark menu meta meter nav noscript
  object ol optgroup option output p param pre progress q rp rt ruby
  s samp script section select small source span strong style sub summary sup
  table tbody td textarea tfoot th thead time title tr track
  u ul var video wbr
]> then let tailless = tag of /* void elements */ <[
    area base br col command embed hr img input
    keygen link meta param source track wbr
  ]>
  # `@a href: url, ->` -> `@element \a, [href: url, ->]`
  ckup[tag] = !-> @element tag, @@, tailless

## Aliases
ckup{quote: $, raw: R, text: T, entity: E, var: v} = ckup
